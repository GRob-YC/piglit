# According to GL 3.2 section 2.11.6 ("Varying Variables") and
# ARB_tessellation_shader
#
#  All components of any varying and special variable written are counted
# against the gl_MaxTessEvaluationOutputComponents limit.
#  All components of any varying and special variable read are counted
# against the gl_MaxFragmentInputComponents limit.
#  gl_Position is a special variables, thus it takes 4 components.

[require]
GLSL >= 1.50
GL_ARB_tessellation_shader


[vertex shader passthrough]

[tessellation control shader]
#version 150
#extension GL_ARB_tessellation_shader: require
layout(vertices = 3) out;

void main() {
	gl_out[gl_InvocationID].gl_Position = gl_in[gl_InvocationID].gl_Position;
	gl_TessLevelOuter = float[4](1.0, 1.0, 1.0, 0.0);
	gl_TessLevelInner = float[2](0.0, 0.0);
}


[tessellation evaluation shader]
#version 150
#extension GL_ARB_tessellation_shader: require

layout(triangles) in;

flat out ivec4 f[min(gl_MaxTessEvaluationOutputComponents - 4, gl_MaxFragmentInputComponents) / 4];

void main() {
	gl_Position = gl_in[0].gl_Position * gl_TessCoord[0]
				+ gl_in[1].gl_Position * gl_TessCoord[1]
				+ gl_in[2].gl_Position * gl_TessCoord[2];

	for (int i = 0; i < f.length(); i++)
		f[i] = ivec4(i * 4, i * 4 + 1, i * 4 + 2, i * 4 + 3);
}


[fragment shader]
#version 150
#extension GL_ARB_tessellation_shader: require

flat in ivec4 f[min(gl_MaxTessEvaluationOutputComponents - 4, gl_MaxFragmentInputComponents) / 4];

void main()
{
	bool ok = true;
	for (int i = 0; i < f.length(); i++) {
		if (f[i] != ivec4(i * 4, i * 4 + 1, i * 4 + 2, i * 4 + 3))
			ok = false;
	}

	gl_FragColor = ok ? vec4(0.0, 1.0, 0.0, 1.0) : vec4(1.0, 0.0, 0.0, 1.0);
}

[vertex data]
piglit_vertex/float/2
-1.0 -1.0
 1.0 -1.0
-1.0  1.0
-1.0  1.0
 1.0 -1.0
 1.0  1.0

[test]
clear color 0.1 0.1 0.1 0.1
clear
patch parameter vertices 3
draw arrays GL_PATCHES 0 6
probe all rgba 0.0 1.0 0.0 1.0
